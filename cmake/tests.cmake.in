function(catkin_initialize_tests)
  if(ENV{CATKIN_TEST_RESULTS_DIR})
    message("Taking test results directory from CATKIN_TEST_RESULTS_DIR")
    set(CATKIN_TEST_RESULTS_DIR $ENV{CATKIN_TEST_RESULTS_DIR} CACHE INTERNAL "")
  elseif(ENV{ROS_TEST_RESULTS_DIR})
    message(WARNING "Taking test results directory from ROS_TEST_RESULTS_DIR; you should use CATKIN_TEST_RESULTS_DIR instead")
    set(CATKIN_TEST_RESULTS_DIR $ENV{ROS_TEST_RESULTS_DIR} CACHE INTERNAL "")
  else()
    set(CATKIN_TEST_RESULTS_DIR ${CMAKE_BINARY_DIR}/test_results CACHE INTERNAL "")
  endif()

  if(NOT TARGET tests)
    add_custom_target(tests)
    message("TODO: implement add_roslaunch_check() in rostest-extras.cmake.")
  endif()
  if(NOT TARGET test)
    add_custom_target(test)
    add_dependencies(test tests)
  endif()
  if(NOT TARGET clean-test-results)
    # Clean out previous test results before running tests.
    add_custom_target(clean-test-results
      COMMAND ${CMAKE_COMMAND} -E remove_directory ${CATKIN_TEST_RESULTS_DIR}
      )
    # Make the tests target depend on clean-test-results, which will ensure
    # that test results are deleted before we try to build tests, and thus
    # before we try to run tests.
    add_dependencies(tests clean-test-results)
  endif()
  if(catkin_SOURCE_DIR)
    find_program_required(CHECK_TEST_RAN_EXE check_test_ran.py
                          PATHS @PROJECT_SOURCE_DIR@/cmake
                          NO_DEFAULT_PATH)
  else()
    find_program_required(CHECK_TEST_RAN_EXE check_test_ran.py
                          PATHS @CMAKE_INSTALL_PREFIX@/share/catkin/cmake
                          NO_DEFAULT_PATH)
  endif()
endfunction()

catkin_initialize_tests()

# This is a macro to ensure that ${PROJECT_NAME}_CACHE gets set in a higher
# scope where we can check it later.
macro(append_test_to_cache CACHENAME)
  set(cachefile ${PROJECT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}.${CACHENAME})
  if(NOT ${PROJECT_NAME}_CACHE)
    file(WRITE ${cachefile} "#\n# This is ${cachefile}\n#\n")
    # Write out the directory that we'll use as CWD later when running
    # tests
    file(APPEND ${cachefile} "${PROJECT_SOURCE_DIR}\n")
    set(${PROJECT_NAME}_CACHE TRUE)
    set(${PROJECT_NAME}_CACHE TRUE PARENT_SCOPE)
    # One test target per project
    add_custom_target(${PROJECT_NAME}_run_tests
      COMMAND ${CATKIN_ENV} ${PYTHON_EXECUTABLE}
      ${catkin_EXTRAS_DIR}/test/runtests.py ${cachefile})
    add_dependencies(test ${PROJECT_NAME}_run_tests)
    add_dependencies(${PROJECT_NAME}_run_tests tests)
  endif()

  # gotcha:  you need a newline or the message doesn't appear
  # in the cache.   pfft.
  file(APPEND ${cachefile} "${ARGN}\n")
endmacro()

# Function to download data on the tests target
function(download_test_data _url _filename _md5)
  # Create a legal target name, in case the target name has slashes in it
  string(REPLACE "/" "_" _testname download_data_${_filename})
  add_custom_command(OUTPUT ${PROJECT_BINARY_DIR}/${_filename}
                     COMMAND ${catkin_EXTRAS_DIR}/test/download_checkmd5.py ${_url} ${PROJECT_BINARY_DIR}/${_filename} ${_md5}
                     VERBATIM)
  add_custom_target(${_testname} DEPENDS ${PROJECT_BINARY_DIR}/${_filename})
  add_dependencies(tests ${_testname})
endfunction()

function(add_gtest exe)
  # Look for optional TIMEOUT argument, #2645
  parse_arguments(_gtest "TIMEOUT;WORKING_DIRECTORY" "" ${ARGN})
  if(_gtest_TIMEOUT)
    message(WARNING "TIMEOUT argument to add_gtest() is ignored")
  endif()
  if(_gtest_WORKING_DIRECTORY)
    set(_chdir_prefix "bash -c \"cd ${_gtest_WORKING_DIRECTORY} && ")
    set(_chdir_suffix "\"")
  endif()

  # Create the program, with basic + gtest build flags
  if(NOT DEFINED GTEST_FOUND)
    message(WARNING "Gtest environment has not been configured.")
  endif()
  include_directories(${GTEST_INCLUDE_DIRS})
  add_executable(${exe} EXCLUDE_FROM_ALL ${_gtest_DEFAULT_ARGS})
  target_link_libraries(${exe} ${GTEST_LIBRARIES} ${THREADS_LIBRARY})

  # Make sure the executable is built before running tests
  add_dependencies(tests ${exe})

  # Create a legal test name, in case the target name has slashes in it
  string(REPLACE "/" "_" _testname ${exe})
  get_target_property(_exe_path ${exe} RUNTIME_OUTPUT_DIRECTORY)
  # We use rosunit to call the executable to get process control, #1629, #3112
  append_test_to_cache(catkin-tests "${_chdir_prefix}${_exe_path}/${exe} --gtest_output=xml:${CATKIN_TEST_RESULTS_DIR}/${PROJECT_NAME}/TEST-${_testname}.xml${_chdir_suffix}")
  append_test_to_cache(catkin-tests "${CHECK_TEST_RAN_EXE} ${CATKIN_TEST_RESULTS_DIR}/${PROJECT_NAME}/TEST-${_testname}.xml")
endfunction()

function(add_nosetests dir)
  # Check that nosetests is installed.
  find_program(nosetests_path nosetests)
  if(NOT nosetests_path)
    # Maybe it has version style naming
    find_program(nosetests_path nosetests2)
      if(NOT nosetests_path)
        message(FATAL_ERROR "Can't find nosetests executable... try installing package 'python-nose'")
    endif()
  endif()

  parse_arguments(_nose "WORKING_DIRECTORY" "" ${ARGN})
  if(_nose_WORKING_DIRECTORY)
    set(_chdir_prefix "bash -c \"cd ${_nose_WORKING_DIRECTORY} && ")
    set(_chdir_suffix "\"")
  endif()

  # Check that the directory exists
  set(_dir_name _dir_name-NOTFOUND)
  if(IS_ABSOLUTE ${dir})
    set(_dir_name ${dir})
  else()
    find_file(_dir_name ${dir}
              PATHS ${CMAKE_CURRENT_SOURCE_DIR}
              NO_DEFAULT_PATH
              NO_CMAKE_FIND_ROOT_PATH)  # for cross-compilation.  thanks jeremy.
    if(NOT _dir_name)
      message(FATAL_ERROR "Can't find nosetests dir \"${dir}\"")
    endif()
  endif()

  # We look for ROS_TEST_COVERAGE=1
  # to indicate that coverage reports are being requested.
  if("$ENV{ROS_TEST_COVERAGE}" STREQUAL "1")
    set(_covarg "--with-coverage")
  else()
    set(_covarg)
  endif()

  set(output_dir_name ${CATKIN_TEST_RESULTS_DIR}/${PROJECT_NAME})
  append_test_to_cache(catkin-tests "${CMAKE_COMMAND} -E make_directory ${output_dir_name}")
  string(REPLACE "/" "." output_file_name ${dir})
  append_test_to_cache(catkin-tests "${_chdir_prefix}${nosetests_path} --where=${_dir_name} --with-xunit --xunit-file=${output_dir_name}/${output_file_name}.xml ${_covarg}${_chdir_suffix}")
  append_test_to_cache(catkin-tests "${CHECK_TEST_RAN_EXE} ${output_dir_name}/${output_file_name}.xml")
endfunction()
